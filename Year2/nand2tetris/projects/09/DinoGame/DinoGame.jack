
class DinoGame {

    field Dino dino;
    field Cactus cactus;
    field Hawk hawk;
    field boolean gravity;
    field boolean jump;
    field int cactusXPos;
    field int hawkXPos;
    field int dinoYPos;

    constructor DinoGame new() {
        do Screen.setColor(true);
        do Screen.drawRectangle(1, 230, 510, 232);
        let dino = Dino.new();
        let cactus = Cactus.new(50);
        let hawk = Hawk.new(50);
        let jump = false;
        let gravity = false;
        return this;
    }

    method int run() {
        var char key;  // the key currently pressed by the user
        var boolean exit;
        var boolean shouldMoveCactusLeft;
        var boolean shouldMoveHawkLeft;
        var boolean didGameOver;
        var int cactusCounter;

        let cactusXPos = 0;
        let hawkXPos = 0;
        let cactusCounter = 0;
        let dinoYPos = 0;
        let exit = false;
        let shouldMoveCactusLeft = true;
        let shouldMoveHawkLeft = false;
        let didGameOver = false;
        
      
        while (~exit) {
            //  waits for a key to be pressed
            while (key = 0) {
                let key = Keyboard.keyPressed();
                if (shouldMoveCactusLeft) {
                    do cactus.moveLeft();
                }

                if (shouldMoveHawkLeft) {
                    do hawk.moveLeft();
                }

                do dino.jump(jump);
                do dino.gravity(gravity);

                let dinoYPos = dino.getYPos();
                let cactusXPos = cactus.getXPos();
                if (cactusXPos > 380) {
                    if (cactusXPos < 420) {
                        if (dinoYPos < 35) {
                            if (~didGameOver) {
                                do Output.printString("GAME OVER ->");
                                do Output.printInt(cactusCounter);
                                let didGameOver = true;
                                do cactus.erase();
                                do dino.dispose();
                                let shouldMoveCactusLeft = false;
                            }
                        }
                    }
                }
                
                if (shouldMoveHawkLeft) {
                    let hawkXPos = hawk.getXPos();
                    if (hawkXPos > 380) {
                        if (hawkXPos < 420) {
                            if (dinoYPos > 2) {
                                if (~didGameOver) {
                                    do Output.printString("GAME OVER ->");
                                    do Output.printInt(cactusCounter);
                                    let didGameOver = true;
                                    do cactus.erase();
                                    do dino.dispose();
                                    let shouldMoveHawkLeft = false;
                                }
                            }
                        }
                    }
                }
                
                do Screen.setColor(false);
                do Screen.drawRectangle(1, 230, 510, 232);
                do Screen.setColor(true);
                do Screen.drawRectangle(1, 230, 510, 232);
                if (dinoYPos < 0) {
                    let jump = false;
                    let gravity = false;
                }

                if (dinoYPos > 80) {
                    let jump = false;
                    let gravity = true;
                }

                if (450 - cactusXPos < 10) {
                    do cactus.erase();
                    if (cactusCounter < 2) {
                        do cactus.setXPos(0);
                    }
                    let cactusCounter = cactusCounter + 1;
                    if (cactusCounter > 1) {
                        do hawk.draw();
                        let shouldMoveHawkLeft = true;
                        let shouldMoveCactusLeft = false;
                    }
                }

                if (450 - hawkXPos < 10) {
                    do hawk.erase();
                    do hawk.setXPos(0);
                }

            }

            if (key = 81)  { let exit = true; }// q key
            if (key = 131) { let jump = true; }// up arrow
            if (key = 32) { let jump = true; }// space key
            // waits for the key to be released
            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
                if (didGameOver){
                    do Screen.clearScreen();
                    do dino.setYPos(0);
                    do cactus.setXPos(0);
                    let didGameOver = false;
                    let shouldMoveCactusLeft = true;
                    // let cactusCounter = 0;
                }
                            
            }
        } // while
        return cactusCounter;
    }

    method void dispose() {
      do Memory.deAlloc(this);
      return;
    }

}