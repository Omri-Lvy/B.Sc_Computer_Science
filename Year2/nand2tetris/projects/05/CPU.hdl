// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    // check if A-instructions
    Not(in=instruction[15], out=isAInstruction); 
    Mux16(a=aluOutToAReg, b=instruction, sel=isAInstruction, out=mux1Out);

    Or(a=isAInstruction, b=instruction[5], out=aRegisterLoad); // check if load A-Register
    ARegister(in=mux1Out, load=aRegisterLoad, out=aRegister, out[0..14]=addressM); // set value of A-register
    
    And(a=instruction[15], b=instruction[12], out=isCInstruction); // check if C-instructions
    Mux16(a=aRegister, b=inM, sel=isCInstruction, out=muxToAlu); // select if send A-register or M-input to ALU
    
    // ALU
    ALU(x=dRegister, y=muxToAlu, zx=instruction[11], nx=instruction[10], zy=instruction[9], 
        ny=instruction[8], f=instruction[7], no=instruction[6], 
        out=aluOutToAReg, out=aluOutToDReg, out=outM, zr=aluZr, ng=aluNg);
    
    And(a=instruction[15], b=instruction[4], out=dRegisterLoad); // check if load D-Register
    DRegister(in=aluOutToDReg, load=dRegisterLoad, out=dRegister); // set value to D-register
    
    And(a=instruction[3], b=instruction[15], out=writeM); // check if write to memory

    // check if jump
    Not(in=aluNg, out=notAluNeg);
    Not(in=aluZr, out=notAluZr);
    And(a=notAluZr, b=aluNg, out=isNegative);
    And(a=notAluZr, b=notAluNeg, out=isPositive);
    And(a=isPositive, b=instruction[0], out=jgt);
    And(a=aluZr, b=instruction[1], out=jeq);
    And(a=isNegative, b=instruction[2], out=jlt);
    Or(a=jgt, b=jeq, out=greaterEqualThan0);
    Or(a=greaterEqualThan0, b=jlt, out=greaterEqualThan0OrLessEqualThan0);
    And(a=greaterEqualThan0OrLessEqualThan0, b=instruction[15], out=pcLoad);

    // increase or reset pc
    PC(in=aRegister, load=pcLoad, inc=true, reset=reset, out[0..14]=pc); 
}